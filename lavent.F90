!OCL  NOEVAL
SUBROUTINE LAVENT(KLON, YDGMV,KIDIA,KFDIA,KSETTLOFF,LD2TLFF1,PVCRS0,PRDLR0,PDTS2,PRDELP,PEVEL,PATND,&
 & PGMV,PB1,PB2,PWRL9)

!**** *LAVENT*   Semi-Lagrangian scheme.
!                Computation of wind components necessary 
!                to find the SL trajectory.

!     Purpose.
!     --------
!          This subroutine computes the wind components at t
!          at each grid-point of the colocation grid (Gauss
!          grid and poles at all levels). These quantities will
!          be used in routine LARMES to find the semi-Lagrangian
!          trajectory.
!          LVERCOR deep-layer eqns: "(a/rs)*wind" is used
!           instead of wind, and extrapolations are done on "(a/rs)*wind".
!          LRWSDLR deep-layer eqns: "(a/r)*wind" is used
!           instead of wind, and extrapolations are done on "(a/r)*wind".

!**   Interface.
!     ----------
!        *CALL* *LAVENT(..)

!        Explicit arguments :
!        --------------------

!        INPUT:
!          KIDIA       - first element of work.
!          KFDIA     - depth of work.
!          LD2TLFF1  - .T./.F.: Refined treatement of (2*Omega Vec r) at
!                      the origin point when there is t-dt (or t in SL2TL)
!                      physics / Other cases.
!          PVCRS0    - array containing quantities for LVERCOR=T (for ex. rs/a) at t.
!          PRDLR0    - array containing "r/a", "a/r", "grad(r/a)" at t.
!          PDTS2     - 0.5*(time step) for the first time-integration step of
!                      a leap-frog scheme or all time-integration steps of
!                      a two-time level scheme; time step for the following
!                      time-integration steps of a leap-frog scheme.
!          PRDELP    - "1/(pressure depth of layers)" at t.
!          PEVEL     - "etadot (d prehyd/d eta)" at t.
!          PATND     - adiabatic Lagrangian tendencies.

!        INPUT/OUTPUT:
!          PGMV      - GMV variables at t-dt and t.
!          PB1       - "SLBUF1" buffer for interpolations.

!        OUTPUT:
!          PB2       - "SLBUF2" buffer.
!          KSETTLOFF - counter for SETTLSTF=T (# of points new scheme activated at each lev)
!          PWRL9     - vertical velocity at t - dt. (to be eventually stored in trajectory)

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!           none
!           Called by LACDYN.

!     Reference.
!     ----------
!             Arpege documentation about semi-Lagrangian scheme.

!     Author.
!     -------
!      K. YESSAD (METEO FRANCE/CNRM/GMAP) after old loops 3202 and 3212 of LACDYN.
!      Original : AUGUST 1995.

! Modifications
! -------------
!      Mar-2002 J. Vivoda  PC schemes for NH dynamics (LPC_XXXX keys)
!   12-Oct-2002 J. Masek   PC bugfix
!   01-Oct-2003 M. Hamrud  CY28 Cleaning
!   M.Hortal 01-Dec-2003 Extra-fields from the dynamics
!   M.Hortal 01-Dec-2003 Vertical trajectory smoothing
!   09-Jun-2004 J. Masek   NH cleaning (LDFULLIMP)
!   01-Jul-2004 K. Yessad  Make clearer the tests for PC scheme.
!   15-Sep-2004 Y. Seity : Add LSQUALL case for AROME academic 2D squall line
!   26-Nov-2007 N. Wedi  : bug correction
!   N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!   K. Yessad Aug 2008: rationalisation of dummy argument interfaces + optimis.
!   F. Vana  15-Oct-2009: NSPLTHOI option
!   K. Yessad Nov 2009: rm case (lsettlst,lpc_nesct)=(f,f) for sl2tl
!   K. Yessad (Jan 2011): introduce INTDYN_MOD structures.
!   F. Vana  22-Feb-2011: NWLAG=4
!   K. Yessad (Nov 2011): use PATND for RHS of wind equation.
!   K. Yessad (Oct 2013): allow NESCT or SETTLST for predictor of LPC_CHEAP.
!   K. Yessad (Oct 2013): use NESCV or SETTLSV for vertical displacement.
!   K. Yessad (July 2014): Rename some variables, move some variables.
!   M. Diamantakis (Feb 2014): code for LSETTLSVF=T (SETTLST filter)
!   O. Marsden: June 2015 CY42 YRGMV, YRGFL, YRSURF, YRGMV5, and YRGFL5 are now passed by argument
! End Modifications
!------------------------------------------------------------------------------

USE YOMGMV   , ONLY : TGMV
USE PARKIND1 , ONLY : JPIM, JPRB

USE YOMCST   , ONLY : RA
USE YOMCT0   , ONLY : LAROME, LTWOTL
USE YOMCT3   , ONLY : NSTEP
USE YOMCVER  , ONLY : LVERTFE
USE YOMDIM   , ONLY : YRDIM
USE YOMDIMV  , ONLY : YRDIMV
USE YOMDYNA  , ONLY : LVERCOR, LRWSDLR, LPC_FULL, LNESCT, LNESCV, LELTRA, LSETTLST, LSETTLSV
USE YOMDYN   , ONLY : YRDYN
USE YOMVERT  , ONLY : YRVETA
USE YOMPARAR , ONLY : YRPARAR
USE PTRSLB1  , ONLY : YRPTRSLB1
USE PTRSLB2  , ONLY : YRPTRSLB2
USE PAR_RDLR , ONLY : JP_ASRF, JP_DIMR0
USE INTDYN_MOD,ONLY : YYTVC0, YYTTND

!     ------------------------------------------------------------------

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
TYPE(TGMV) , INTENT(INOUT) :: YDGMV
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KSETTLOFF(YRDIMV%NFLEVG)
LOGICAL           ,INTENT(IN)    :: LD2TLFF1 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVCRS0(YRDIM%NPROMVC,0:YRDIMV%NFLEVG,YYTVC0%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDLR0(YRDIM%NPROMDLR,0:YRDIMV%NFLEVG,JP_DIMR0)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDTS2 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PEVEL(KLON,0:YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PATND(KLON,YRDIMV%NFLEVG,YYTTND%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMV(KLON,YRDIMV%NFLEVG,YDGMV%NDIMGMV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PB1(KLON,YRPTRSLB1%NFLDSLB1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PB2(KLON,YRPTRSLB2%NFLDSLB2)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PWRL9(KLON,YRDIMV%NFLEVG)
!     ------------------------------------------------------------------
INTEGER(KIND=JPIM) ::JROF, JLEV
INTEGER(KIND=JPIM) :: ISLB1U9, ISLB1V9
REAL(KIND=JPRB) :: ZWRL0(KLON,YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZUSA, ZMAGW0, ZMAGW9, ZWJUMP, ZWAVMAG
LOGICAL :: LLCT, LLSETTLST, LLNESCT, LLSETTLSV, LLNESCV



!     ------------------------------------------------------------------




!     ------------------------------------------------------------------

ZUSA=1.0_JPRB/RA

LLCT = (LPC_FULL .AND. YRDYN%NCURRENT_ITER > 0)

LLSETTLST=LSETTLST.AND.(.NOT.LELTRA)
LLNESCT=LNESCT
LLSETTLSV=LSETTLSV.AND.(.NOT.LELTRA)
LLNESCV=LNESCV

IF ((YRDYN%NSPLTHOI /= 0).AND.(YRDYN%NWLAG<4)) THEN
  ISLB1U9  = YRPTRSLB1%MSLB1UF9
  ISLB1V9  = YRPTRSLB1%MSLB1VF9
ELSE
  ISLB1U9  = YRPTRSLB1%MSLB1U9
  ISLB1V9  = YRPTRSLB1%MSLB1V9
ENDIF  


!     * Computation of "etadot" at full levels.

IF(LVERTFE) THEN
  ! PEVEL is at full levels.
  DO JLEV=1,YRDIMV%NFLEVG
    DO JROF=KIDIA,KFDIA
      ZWRL0(JROF,JLEV)=PEVEL(JROF,JLEV)&
       & *(YRVETA%VETAH(JLEV)-YRVETA%VETAH(JLEV-1))*PRDELP(JROF,JLEV)
    ENDDO
  ENDDO
ELSE
  ! PEVEL is at half levels.
  DO JLEV=1,YRDIMV%NFLEVG
    DO JROF=KIDIA,KFDIA
      ZWRL0(JROF,JLEV)=&
       & 0.5_JPRB*(PEVEL(JROF,JLEV)+PEVEL(JROF,JLEV-1))&
       & *(YRVETA%VETAH(JLEV)-YRVETA%VETAH(JLEV-1))*PRDELP(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF

!     * Computation of wind components necessary to find the SL trajectory.

IF(LTWOTL.AND.YRDYN%LSVTSM) THEN
  DO JLEV=1,YRDIMV%NFLEVG
    PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1WRA+JLEV-YRDIMV%NFLSA)=ZWRL0(KIDIA:KFDIA,JLEV)
  ENDDO
ENDIF


IF (LTWOTL.AND.LELTRA) THEN

  ! Use the RHS of momentum equation.
  IF (LVERCOR) THEN
    DO JLEV=1,YRDIMV%NFLEVG
      DO JROF=KIDIA,KFDIA
        PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)* &
         & (PGMV(JROF,JLEV,YDGMV%YT0%MU)+0.5_JPRB*PB1(JROF,ISLB1U9+JLEV-YRDIMV%NFLSA)) &
         & +PDTS2*PATND(JROF,JLEV,YYTTND%M_TNDASRU) 
        PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)* &
         & (PGMV(JROF,JLEV,YDGMV%YT0%MV)+0.5_JPRB*PB1(JROF,ISLB1V9+JLEV-YRDIMV%NFLSA)) &
         & +PDTS2*PATND(JROF,JLEV,YYTTND%M_TNDASRV) 
      ENDDO
    ENDDO
  ELSEIF (LRWSDLR) THEN
    DO JLEV=1,YRDIMV%NFLEVG
      DO JROF=KIDIA,KFDIA
        PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PRDLR0(JROF,JLEV,JP_ASRF)* &
         & (PGMV(JROF,JLEV,YDGMV%YT0%MU)+0.5_JPRB*PB1(JROF,ISLB1U9+JLEV-YRDIMV%NFLSA)) &
         & +PDTS2*PATND(JROF,JLEV,YYTTND%M_TNDASRU)
        PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PRDLR0(JROF,JLEV,JP_ASRF)* &
         & (PGMV(JROF,JLEV,YDGMV%YT0%MV)+0.5_JPRB*PB1(JROF,ISLB1V9+JLEV-YRDIMV%NFLSA)) &
         & +PDTS2*PATND(JROF,JLEV,YYTTND%M_TNDASRV)
      ENDDO
    ENDDO
  ELSE
    DO JLEV=1,YRDIMV%NFLEVG
      DO JROF=KIDIA,KFDIA
        PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)= &
         & PGMV(JROF,JLEV,YDGMV%YT0%MU)+0.5_JPRB*PB1(JROF,ISLB1U9+JLEV-YRDIMV%NFLSA) &
         & +PDTS2*PATND(JROF,JLEV,YYTTND%M_TNDU)
        PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)= &
         & PGMV(JROF,JLEV,YDGMV%YT0%MV)+0.5_JPRB*PB1(JROF,ISLB1V9+JLEV-YRDIMV%NFLSA) &
         & +PDTS2*PATND(JROF,JLEV,YYTTND%M_TNDV)
      ENDDO
    ENDDO
  ENDIF

  DO JLEV=1,YRDIMV%NFLEVG
    PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)=ZWRL0(KIDIA:KFDIA,JLEV)
    PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2URL+JLEV-1)=PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)
    PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2VRL+JLEV-1)=PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)
    PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2WRL+JLEV-1)=PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)
  ENDDO

  IF (LD2TLFF1) THEN
    DO JLEV=1,YRDIMV%NFLEVG
      PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MU)
      PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MV)
    ENDDO
    IF (LVERCOR) THEN
      DO JLEV=1,YRDIMV%NFLEVG
        DO JROF=KIDIA,KFDIA
          PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA) &
           & *PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
          PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA) &
           & *PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
        ENDDO
      ENDDO
    ELSEIF (LRWSDLR) THEN
      DO JLEV=1,YRDIMV%NFLEVG
        DO JROF=KIDIA,KFDIA
          PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA) &
           & *PRDLR0(JROF,JLEV,JP_ASRF)
          PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA) &
           & *PRDLR0(JROF,JLEV,JP_ASRF)
        ENDDO
      ENDDO
    ENDIF
  ENDIF

ELSEIF (LTWOTL.AND.(.NOT.LELTRA)) THEN

  IF (.NOT.LLCT) THEN

    ! * Cases:
    !   NSITER=0
    !   Predictor step of LPC_FULL

    ! -- Horizontal displacement:
    IF ( NSTEP <= 0 .OR. LLNESCT ) THEN
      ! * non-extrapolating scheme X(t+dt) = X(t) 
      IF (LVERCOR) THEN
        DO JLEV=1,YRDIMV%NFLEVG
          DO JROF=KIDIA,KFDIA
            PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT0%MU) &
             & *PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
            PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT0%MV) &
             & *PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
          ENDDO
        ENDDO
      ELSEIF (LRWSDLR) THEN
        DO JLEV=1,YRDIMV%NFLEVG
          DO JROF=KIDIA,KFDIA
            PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT0%MU) &
             & *PRDLR0(JROF,JLEV,JP_ASRF)
            PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT0%MV) &
             & *PRDLR0(JROF,JLEV,JP_ASRF)
          ENDDO
        ENDDO
      ELSE
        DO JLEV=1,YRDIMV%NFLEVG
          PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MU)
          PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MV)
        ENDDO
      ENDIF
      DO JLEV=1,YRDIMV%NFLEVG
        PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2URL+JLEV-1)=PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)
        PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2VRL+JLEV-1)=PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)
      ENDDO
    ELSEIF (LLSETTLST) THEN
      ! * SETTLS-extrapolating schemes X(t+dt) = f( X(t),X(t-dt) )
      IF (LVERCOR) THEN
        DO JLEV=1,YRDIMV%NFLEVG
          DO JROF=KIDIA,KFDIA
            PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=&
             & 2.0_JPRB*PGMV(JROF,JLEV,YDGMV%YT0%MU)*PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)&
             & -PGMV(JROF,JLEV,YDGMV%YT9%MU)*PGMV(JROF,JLEV,YDGMV%YT9%MVCASRS)
            PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=&
             & 2.0_JPRB*PGMV(JROF,JLEV,YDGMV%YT0%MV)*PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)&
             & -PGMV(JROF,JLEV,YDGMV%YT9%MV)*PGMV(JROF,JLEV,YDGMV%YT9%MVCASRS)
          ENDDO
        ENDDO
      ELSEIF (LRWSDLR) THEN
        DO JLEV=1,YRDIMV%NFLEVG
          DO JROF=KIDIA,KFDIA
            PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=&
             & 2.0_JPRB*PGMV(JROF,JLEV,YDGMV%YT0%MU)*PRDLR0(JROF,JLEV,JP_ASRF)&
             & -PGMV(JROF,JLEV,YDGMV%YT9%MU)*PGMV(JROF,JLEV,YDGMV%YT9%MRDLASR)
            PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=&
             & 2.0_JPRB*PGMV(JROF,JLEV,YDGMV%YT0%MV)*PRDLR0(JROF,JLEV,JP_ASRF)&
             & -PGMV(JROF,JLEV,YDGMV%YT9%MV)*PGMV(JROF,JLEV,YDGMV%YT9%MRDLASR)
          ENDDO
        ENDDO
      ELSE
        DO JLEV=1,YRDIMV%NFLEVG
          DO JROF=KIDIA,KFDIA
            PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=2.0_JPRB*PGMV(JROF,JLEV,YDGMV%YT0%MU)&
             & -PGMV(JROF,JLEV,YDGMV%YT9%MU)
            PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=2.0_JPRB*PGMV(JROF,JLEV,YDGMV%YT0%MV)&
             & -PGMV(JROF,JLEV,YDGMV%YT9%MV)
          ENDDO
        ENDDO
      ENDIF
      IF (LVERCOR) THEN
        DO JLEV=1,YRDIMV%NFLEVG
          DO JROF=KIDIA,KFDIA
            PB2(JROF,YRPTRSLB2%MSLB2URL+JLEV-1)=PGMV(JROF,JLEV,YDGMV%YT0%MU) &
             & *PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
            PB2(JROF,YRPTRSLB2%MSLB2VRL+JLEV-1)=PGMV(JROF,JLEV,YDGMV%YT0%MV) &
             & *PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
          ENDDO
        ENDDO
      ELSEIF (LRWSDLR) THEN
        DO JLEV=1,YRDIMV%NFLEVG
          DO JROF=KIDIA,KFDIA
            PB2(JROF,YRPTRSLB2%MSLB2URL+JLEV-1)=PGMV(JROF,JLEV,YDGMV%YT0%MU) &
             & *PRDLR0(JROF,JLEV,JP_ASRF)
            PB2(JROF,YRPTRSLB2%MSLB2VRL+JLEV-1)=PGMV(JROF,JLEV,YDGMV%YT0%MV) &
             & *PRDLR0(JROF,JLEV,JP_ASRF)
          ENDDO
        ENDDO
      ELSE
        DO JLEV=1,YRDIMV%NFLEVG
          PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2URL+JLEV-1)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MU)
          PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2VRL+JLEV-1)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MV)
        ENDDO
      ENDIF
    ENDIF
    IF (LVERCOR) THEN
      PGMV(KIDIA:KFDIA,1:YRDIMV%NFLEVG,YDGMV%YT9%MVCASRS)=PVCRS0(KIDIA:KFDIA,1:YRDIMV%NFLEVG,YYTVC0%M_VCASRSF)
    ELSEIF (LRWSDLR) THEN
      PGMV(KIDIA:KFDIA,1:YRDIMV%NFLEVG,YDGMV%YT9%MRDLASR)=PRDLR0(KIDIA:KFDIA,1:YRDIMV%NFLEVG,JP_ASRF)
    ENDIF

    ! -- Vertical displacement:
    IF ( NSTEP <= 0 .OR. LLNESCV ) THEN
      ! * non-extrapolating scheme X(t+dt) = X(t) 
      DO JLEV=1,YRDIMV%NFLEVG
        PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)=ZWRL0(KIDIA:KFDIA,JLEV)
        PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2WRL+JLEV-1)=PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)
      ENDDO
      IF (NSTEP == 0 .AND. YRDYN%LSETTLSVF) PWRL9(KIDIA:KFDIA,1:YRDIMV%NFLEVG)=0.0_JPRB
    ELSEIF (LLSETTLSV) THEN
      ! * SETTLS-extrapolating schemes X(t+dt) = f( X(t),X(t-dt) )
      IF (YRDYN%LSETTLSVF) THEN
! detect noisy gridpoints which should not be extrapolated
        DO JLEV=1,YRDYN%NFLEVSF
          KSETTLOFF(JLEV)=0
          DO JROF=KIDIA,KFDIA
        ! this is equal to 1-2-1 average for tn-1, tn, tn+1 etadot values where tn+1 is obtained
        ! with 2nd order t-extrapolation 
            ZMAGW0=ABS(ZWRL0(JROF,JLEV))
            ZMAGW9=ABS(PGMV(JROF,JLEV,YDGMV%YT9%MEDOT))
            ZWAVMAG=YRDYN%RSETTLF(JLEV)*(ZMAGW0+ZMAGW9)
            ZWJUMP=ABS(PGMV(JROF,JLEV,YDGMV%YT9%MEDOT)-ZWRL0(JROF,JLEV))
            IF (ZWJUMP>ZWAVMAG+YRDYN%REPSDYN) THEN
              IF (YRDYN%LSETFSTAT) KSETTLOFF(JLEV)=KSETTLOFF(JLEV)+1
              PB1(JROF,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)=ZWRL0(JROF,JLEV)
            ELSE
              PB1(JROF,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)=2.0_JPRB*ZWRL0(JROF,JLEV)&
               & -PGMV(JROF,JLEV,YDGMV%YT9%MEDOT)
            ENDIF
          ENDDO
        ENDDO
        DO JLEV=YRDYN%NFLEVSF+1,YRDIMV%NFLEVG
          DO JROF=KIDIA,KFDIA
            PB1(JROF,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)=2.0_JPRB*ZWRL0(JROF,JLEV)&
             & -PGMV(JROF,JLEV,YDGMV%YT9%MEDOT)
          ENDDO
        ENDDO
        PWRL9(KIDIA:KFDIA,1:YRDIMV%NFLEVG)=PGMV(KIDIA:KFDIA,1:YRDIMV%NFLEVG,YDGMV%YT9%MEDOT)
      ELSE
        DO JLEV=1,YRDIMV%NFLEVG
          DO JROF=KIDIA,KFDIA
            PB1(JROF,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)=2.0_JPRB*ZWRL0(JROF,JLEV)&
             & -PGMV(JROF,JLEV,YDGMV%YT9%MEDOT)
          ENDDO
        ENDDO
      ENDIF
      PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2WRL:YRPTRSLB2%MSLB2WRL+YRDIMV%NFLEVG-1)=ZWRL0(KIDIA:KFDIA,1:YRDIMV%NFLEVG)
    ENDIF
    PGMV(KIDIA:KFDIA,1:YRDIMV%NFLEVG,YDGMV%YT9%MEDOT)=ZWRL0(KIDIA:KFDIA,1:YRDIMV%NFLEVG)

  ELSE 

    ! * Corrector step of LPC_FULL:

    IF (LVERCOR) THEN
      DO JLEV=1,YRDIMV%NFLEVG
        DO JROF=KIDIA,KFDIA
          PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT9%MU) &
           & *PGMV(JROF,JLEV,YDGMV%YT9%MVCASRS)
          PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT9%MV) &
           & *PGMV(JROF,JLEV,YDGMV%YT9%MVCASRS)
        ENDDO
      ENDDO
    ELSEIF (LRWSDLR) THEN
      DO JLEV=1,YRDIMV%NFLEVG
        DO JROF=KIDIA,KFDIA
          PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT9%MU) &
           & *PGMV(JROF,JLEV,YDGMV%YT9%MRDLASR)
          PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT9%MV) &
           & *PGMV(JROF,JLEV,YDGMV%YT9%MRDLASR)
        ENDDO
      ENDDO
    ELSE
      DO JLEV=1,YRDIMV%NFLEVG
        PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT9%MU)
        PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT9%MV)
      ENDDO
    ENDIF
    DO JLEV=1,YRDIMV%NFLEVG
      PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT9%MEDOT)
    ENDDO

    IF (LVERCOR) THEN
      DO JLEV=1,YRDIMV%NFLEVG
        DO JROF=KIDIA,KFDIA
          PB2(JROF,YRPTRSLB2%MSLB2URL+JLEV-1)=PGMV(JROF,JLEV,YDGMV%YT0%MU)*PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
          PB2(JROF,YRPTRSLB2%MSLB2VRL+JLEV-1)=PGMV(JROF,JLEV,YDGMV%YT0%MV)*PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
        ENDDO
      ENDDO
    ELSEIF (LRWSDLR) THEN
      DO JLEV=1,YRDIMV%NFLEVG
        DO JROF=KIDIA,KFDIA
          PB2(JROF,YRPTRSLB2%MSLB2URL+JLEV-1)=PGMV(JROF,JLEV,YDGMV%YT0%MU) &
           & *PRDLR0(JROF,JLEV,JP_ASRF)
          PB2(JROF,YRPTRSLB2%MSLB2VRL+JLEV-1)=PGMV(JROF,JLEV,YDGMV%YT0%MV) &
           & *PRDLR0(JROF,JLEV,JP_ASRF)
        ENDDO
      ENDDO
    ELSE
      DO JLEV=1,YRDIMV%NFLEVG
        PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2URL+JLEV-1)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MU)
        PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2VRL+JLEV-1)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MV)
      ENDDO
    ENDIF
    DO JLEV=1,YRDIMV%NFLEVG
      PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2WRL+JLEV-1)=ZWRL0(KIDIA:KFDIA,JLEV)
    ENDDO

  ENDIF

  IF (LD2TLFF1) THEN
    DO JLEV=1,YRDIMV%NFLEVG
      PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MU)
      PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MV)
    ENDDO
    IF (LVERCOR) THEN
      DO JLEV=1,YRDIMV%NFLEVG
        DO JROF=KIDIA,KFDIA
          PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA) &
           & *PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
          PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA) &
           & *PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
        ENDDO
      ENDDO
    ELSEIF (LRWSDLR) THEN
      DO JLEV=1,YRDIMV%NFLEVG
        DO JROF=KIDIA,KFDIA
          PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA) &
           & *PRDLR0(JROF,JLEV,JP_ASRF)
          PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA) &
           & *PRDLR0(JROF,JLEV,JP_ASRF)
        ENDDO
      ENDDO
    ENDIF
  ENDIF

ELSE

  ! SL3TL:

  IF (LVERCOR) THEN
    DO JLEV=1,YRDIMV%NFLEVG
      DO JROF=KIDIA,KFDIA
        PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT0%MU) &
         & *PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
        PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT0%MV) &
         & *PVCRS0(JROF,JLEV,YYTVC0%M_VCASRSF)
      ENDDO
    ENDDO
  ELSEIF (LRWSDLR) THEN
    DO JLEV=1,YRDIMV%NFLEVG
      DO JROF=KIDIA,KFDIA
        PB1(JROF,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT0%MU) &
         & *PRDLR0(JROF,JLEV,JP_ASRF)
        PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PGMV(JROF,JLEV,YDGMV%YT0%MV) &
         & *PRDLR0(JROF,JLEV,JP_ASRF)
      ENDDO
    ENDDO
  ELSE
    DO JLEV=1,YRDIMV%NFLEVG
      PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MU)
      PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT0%MV)
    ENDDO
  ENDIF
  DO JLEV=1,YRDIMV%NFLEVG
    PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)=ZWRL0(KIDIA:KFDIA,JLEV)
    PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2URL+JLEV-1)=PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1UR0+JLEV-YRDIMV%NFLSA)
    PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2VRL+JLEV-1)=PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)
    PB2(KIDIA:KFDIA,YRPTRSLB2%MSLB2WRL+JLEV-1)=PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1WR0+JLEV-YRDIMV%NFLSA)
  ENDDO

  IF (LD2TLFF1) THEN
    DO JLEV=1,YRDIMV%NFLEVG
      PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT9%MU)
      PB1(KIDIA:KFDIA,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA)=PGMV(KIDIA:KFDIA,JLEV,YDGMV%YT9%MV)
    ENDDO
    IF (LVERCOR) THEN
      DO JLEV=1,YRDIMV%NFLEVG
        DO JROF=KIDIA,KFDIA
          PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA) &
           & *PGMV(JROF,JLEV,YDGMV%YT9%MVCASRS)
          PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA) &
           & *PGMV(JROF,JLEV,YDGMV%YT9%MVCASRS)
        ENDDO
      ENDDO
    ELSEIF (LRWSDLR) THEN
      DO JLEV=1,YRDIMV%NFLEVG
        DO JROF=KIDIA,KFDIA
          PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1UR9+JLEV-YRDIMV%NFLSA) &
           & *PGMV(JROF,JLEV,YDGMV%YT9%MRDLASR)
          PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1VR9+JLEV-YRDIMV%NFLSA) &
           & *PGMV(JROF,JLEV,YDGMV%YT9%MRDLASR)
        ENDDO
      ENDDO
    ENDIF
  ENDIF

ENDIF

IF (LAROME.AND.YRPARAR%LSQUALL) THEN
  DO JLEV=1,YRDIMV%NFLEVG
    DO JROF=KIDIA,KFDIA
      PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)=PB1(JROF,YRPTRSLB1%MSLB1VR0+JLEV-YRDIMV%NFLSA)+YRPARAR%VSQUALL
      PB2(JROF,YRPTRSLB2%MSLB2VRL+JLEV-1)=PB2(JROF,YRPTRSLB2%MSLB2VRL+JLEV-1)+YRPARAR%VSQUALL
    ENDDO
  ENDDO
ENDIF

!     ------------------------------------------------------------------



END SUBROUTINE LAVENT

